plugins {
    id 'java'

}

group 'com.ubnt.discovery'
version '1.3.3'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 13
targetCompatibility = 13

repositories {
    mavenCentral()
}

dependencies {
    // CLI support
    implementation 'com.beust:jcommander:1.72'

    // Library used for displaying SVG images
    implementation 'org.apache.xmlgraphics:batik-all:1.16'

    // Library used for a modern LookAndFeel. If you don't want to use this just
    // leave the ubnt.ui.laf property empty in the ubnt-tool.properties file.
    // Just comment out this line and the library won't be included in the project
    // build.
    implementation 'com.formdev:flatlaf:2.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': 'com.ubnt.discovery.UbntDiscoveryTool')
    }
}

//noinspection ConfigurationAvoidance
task UbntTool(type: Jar) {
    manifest.from jar.manifest
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        // preventing signed jars into the final JAR
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

java {
    withSourcesJar()
    // Comment out the next statement if you don't want ot generate
    // the Javadoc files
    // withJavadocJar()
}

test {
    useJUnitPlatform()
}